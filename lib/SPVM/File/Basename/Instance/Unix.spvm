class File::Basename::Instance::Unix extends File::Basename::Instance {
  use Regex;
  
  # Class Methods
  static method new : File::Basename::Instance::Unix () {
    
    my $self = new File::Basename::Instance::Unix;
    
    return $self;
  }
  
  # Instance Methods
  method fileparse : string[] ($path : string) {
    unless ($path) {
      die "The \$path must be defined";
    }
    
    my $dirpath = (string)undef;
    my $basename = (string)undef;
    
    my $re = Regex->new("(?s)^(.*/)?(.*)");
    if ($re->match($path)) {
      $dirpath = $re->cap1;
      $basename = $re->cap2;
    }
    
    unless (length $dirpath) {
      $dirpath = "./";
    }
    
    return [$basename, $dirpath, ""];
  }
  
  method basename : string ($path : string) {
    unless ($path) {
      die "The \$path must be defined";
    }
    
    $path = $self->(File::Basename::Instance::Interface)->_strip_trailing_sep($path);

    my $fileparse = $self->(File::Basename::Instance::Interface)->fileparse($path);
    
    my $basename = $fileparse->[0];
    my $dirname = $fileparse->[1];

    if (!length $basename) {
      $basename = $dirname;
    }
    
    return $basename;
  }
  
  method dirname : string ($path : string) {
    unless ($path) {
      die "The \$path must be defined";
    }
    
    my $fileparse = $self->(File::Basename::Instance::Interface)->fileparse($path);
    
    my $basename = $fileparse->[0];
    my $dirname = $fileparse->[1];

    $dirname = $self->(File::Basename::Instance::Interface)->_strip_trailing_sep($dirname);
    
    unless (length $basename) {
      my $fileparse_again = $self->(File::Basename::Instance::Interface)->fileparse($dirname);
      $dirname = $fileparse_again->[1];
      $dirname = $self->(File::Basename::Instance::Interface)->_strip_trailing_sep($dirname);
    }

    return $dirname;
  }

  method _strip_trailing_sep : string ($path : string)  {
    
    my $re = Regex->new("(?s)(.)/*\z");
    $path = $re->replace($path, method : string ($re : Regex) {
      return $re->cap1;
    });
    
    return $path;
  }
}
